{------------------------------------------------------------------------------
  Open Source under the MIT License – free to use, modify, and share.
  TradeStation® is a trademark of TradeStation Group, Inc.; all rights reserved.
------------------------------------------------------------------------------}

Inputs:
	Price( Close ),
	
	int BullCrossIdx(1),
	int BearCrossIdx(-1),
	int BullTrendIdx(0),
	int BearTrendIdx(0),
	int Length(20);
	
Variables:  
	Avg( 0 );

// calculate average (basically SMA)
Avg = AverageFC( Price, Length );

Variables:
	bool isBullCross( false ),
	bool isBearCross( false ),
	intrabarpersist bool TrendUp( false ),
	intrabarpersist bool TrendDn( false );

Setplottype( 2, Ptstring );
Setplottype( 3, Ptstring );

Begin
	Plot1( 0, !("MaPriceCrossIdx"), White, Default, 2 );
	
	// specific to radar screen
	Plot2( "", "Trend" );
	Plot3( "", "Alert" );
	Setplotbgcolor( 3, Transparent );
End;

// NOTE bias change (TrendUp/TrendDn) does not actually change till the cross is
// confirmed at the EOD; Radarscreen "Trend" will always reflect this. Indicator
// (MaPriceCrossIdx) and "Alert" will always show the realtime cross.

// plot price biases to ma
If Close > Avg Then
Begin
	Plot1(BullTrendIdx, !("MaPriceCrossIdx"));
	Setplotcolor( 1, Green );
	
	If ( BarStatus( 1 ) = 2 ) then
	Begin
		TrendUp = true;
		TrendDn = false;
	End;
End;

If Close < Avg Then
Begin
	Plot1(BearTrendIdx, !("MaPriceCrossIdx"));
	Setplotcolor( 1, Red );
	
	If ( BarStatus( 1 ) = 2 ) then
	Begin
		TrendUp = false;
		TrendDn = true;
	End;
End;

// NOTE for Radarscreen Trend coloring to work, need to load additional bars
// NOTE this must come after the assignments above, or it will not switch over
// after the close of the market.
If ( TrendUp ) Then
Begin
	Setplotbgcolor( 2, Green );
End;

If ( TrendDn ) Then
Begin
	Setplotbgcolor( 2, Red );
End;

// bull price crossing
isBullCross = Close[1] < Avg[1] And Close > Avg;
If isBullCross = True Then
Begin
	Plot1(BullCrossIdx, !("MaPriceCrossIdx"));
	Setplotcolor( 1, Green );
	
	Plot3( !( "BullCross" ), "Alert" );
	Setplotbgcolor( 3, Blue );
	
	Alert( !("MaPriceCross: BullCross") );
End;
	
// bear price crossing
isBearCross = Close[1] > Avg[1] And Close < Avg;
if isBearCross = True Then
Begin
	Plot1(BearCrossIdx, !("MaPriceCrossIdx"));
	Setplotcolor( 1, Red );
	
	Plot3( !( "BearCross" ), "Alert" );
	Setplotbgcolor( 3, Magenta );
	
	Alert( !("MaPriceCross: BearCross") );
End;

